#pragma kernel DensityCS
#pragma kernel PressureCS
#pragma kernel ForceCS
#pragma kernel ThermalDiffuseCS
#pragma kernel TypeCS
#pragma kernel IntegrateCS

// 簡略化のためにスレッド数は1次元にしておく
#define THREAD_SIZE_X 1024

// ▼ 構造化バッファの定義 ---------------------
struct Particle {
	float3 position;
	float3 velocity;
};

struct ParticleTemperature {
	float temperature;
	float fuel;
};

struct ParticlePressure {
	float pressure;
};

struct ParticleForces {
	float3 acceleration;
};

struct ParticleDensity {
	float density;
};

struct ParticleType {
	int type;//0,1,2
};

struct ParticleThermalDiffuse {
	float thermalDiffuse;
	float fuelDiffuse;
};

// 粒子位置と速度を保持するバッファ
StructuredBuffer  <Particle>        _ParticlesBufferRead;
RWStructuredBuffer<Particle>        _ParticlesBufferWrite;

// 粒子密度を保持するバッファ
StructuredBuffer  <ParticleDensity> _ParticlesDensityBufferRead;
RWStructuredBuffer<ParticleDensity> _ParticlesDensityBufferWrite;

// 粒子圧力を保持するバッファ
StructuredBuffer<ParticlePressure> _ParticlesPressureBufferRead;
RWStructuredBuffer<ParticlePressure> _ParticlesPressureBufferWrite;

// 粒子加速度(力)を保持するバッファ
StructuredBuffer  <ParticleForces>  _ParticlesForceBufferRead;
RWStructuredBuffer<ParticleForces>  _ParticlesForceBufferWrite;

// 粒子の温度データを保持するバッファ
StructuredBuffer<ParticleTemperature>	_ParticlesTemperatureBufferRead;
RWStructuredBuffer<ParticleTemperature> _ParticlesTemperatureBufferWrite;

// 粒子の初期位置データを保持するバッファ
StructuredBuffer<Particle>			_ParticlesInitPosBufferRead;

// 粒子の燃焼判定データを保持するバッファ
StructuredBuffer<ParticleType>		_ParticlesTypeBufferRead;
RWStructuredBuffer<ParticleType>	_ParticlesTypeBufferWrite;

// 粒子の熱拡散データを保持するバッファ
StructuredBuffer<ParticleThermalDiffuse>	_ParticleThermalDiffuseRead;
RWStructuredBuffer<ParticleThermalDiffuse>	_ParticleThermalDiffuseWrite;

// ▲ 構造化バッファの定義 ---------------------

// ▼ シェーダ定数の定義 -----------------------
cbuffer CB {
	int   _NumParticles;		// 粒子数
	float _TimeStep;			// 時間刻み幅(dt)
	float _Smoothlen;			// 粒子半径
	float _PressureStiffness;	// Beckerの係数
	float _RestDensity;			// 静止密度
	float _DensityCoef;			// 密度算出時の係数
	float _GradPressureCoef;	// 圧力算出時の係数
	float _LapViscosityCoef;	// 粘性算出時の係数
	float _WallStiffness;		// ペナルティ法の押し返す力
	float _FuelConsumeFactor;	//燃料消費係数
	float _ReactSpeed;			//化学反応速度係数
	float _TemperatureProduce;	//温度生成係数　(筆者自己定義?) 
	float _AmbientTemperature;	//環境温度
	float _Emissivity;			//物質の放射率
	float _StefanConst;			//ステファン・ボルツマン定数(J/m^3K^4) 
	float _CopperDiffusivity;	//熱拡散率(銅) 100 * pow(10,-6)
	float _FuelDiffuse;			//燃料拡散係数
	float _ParticleMass;		//粒子質量 
	float _StarRadius;          //星の半径  
	float _BurnSpeed;			//星の燃焼速度  
	float _BuoyancyCoef;		//浮力係数 
	float _ParticleDist;		//粒子の初期間隔


	float _Viscosity;			// 粘性係数
	float3 _Gravity;			// 重力
	float3 _Range;				// シミュレーション空間

	float3 _MousePos;			// マウス位置
	float _MouseRadius;			// マウスインタラクションの半径
	bool _MouseDown;			// マウスが押されているか
	bool _IsFirework;			// 浮力の加算を入れるか
};
// ▲ シェーダ定数の定義 -----------------------



// ▼ ★ここから本体 --------------------------------------

/// Poly6 Kernel の実装
/// http://www.slis.tsukuba.ac.jp/~fujisawa.makoto.fu/cgi-bin/wiki/index.php?SPH%CB%A1%A4%CE%BD%C5%A4%DF%B4%D8%BF%F4
/// W_poly6(r, h) = DensityCoef * (h^2 - r^2)^3
/// -> DensityCoef = ParticleMass * 4 / (PI * Smoothlen^8)
inline float CalculateDensity(float r_sq) {
	const float h_sq = _Smoothlen * _Smoothlen;
	return _DensityCoef * (h_sq - r_sq) * (h_sq - r_sq) * (h_sq - r_sq);
}

/// Becker2007 非圧縮条件の実装:
/// Pressure = B * ((rho / rho_0)^gamma  - 1)
/// 圧力定数Bは正確に計算するべきだが、リアルタイム向きではないので適当な値にする
inline float CalculatePressure(float density) {
	return _PressureStiffness * max(pow(density / _RestDensity, 7) - 1, 0);
}

/// Spikyカーネルの実装:
/// mass * (pressure_i- pressure_j)/(2 * density_j) * Grad(W_Spiky)
/// Grad(W_Spiky) = -30 / (pi * h^5) * (h - r)^2
inline float3 CalculateGradPressure(float r, float P_pressure, float N_pressure, float N_density, float3 diff) {
	const float h = _Smoothlen;
	float avg_pressure = 0.5f * (N_pressure + P_pressure);
	return _GradPressureCoef * avg_pressure / N_density * (h - r) * (h - r) / r * (diff);
}

/// Viscosityカーネルの実装:
/// mass * (u_j - u_i) / density_j * Laplacian(W_viscosity)
/// Laplacian(W_viscosity) = 20 / (3 * pi * h^5) * (h - r)
inline float3 CalculateLapVelocity(float r, float3 P_velocity, float3 N_velocity, float N_density) {
	const float h = _Smoothlen;
	float3 vel_diff = (N_velocity - P_velocity);
	return _LapViscosityCoef / N_density * (h - r) * vel_diff;
}



// ▼ カーネル関数

//--------------------------------------------------------------------------------------
// 密度の算出
//--------------------------------------------------------------------------------------
[numthreads(THREAD_SIZE_X, 1, 1)]
void DensityCS(uint3 DTid : SV_DispatchThreadID) {
	uint P_ID = DTid.x;	// 現在処理しているパーティクルID

	float h_sq = _Smoothlen * _Smoothlen;
	float3 P_position = _ParticlesBufferRead[P_ID].position;

	// 近傍探索(O(n^2))
	float density = 0;
	for (uint N_ID = 0; N_ID < _NumParticles; N_ID++) {
		if (N_ID == P_ID) continue;	// 自身を計算対象とした場合スキップ

		float3 N_position = _ParticlesBufferRead[N_ID].position;

		float3 diff = N_position - P_position;	// 粒子距離
		float r_sq = dot(diff, diff);			// 粒子距離の2乗

		// 半径内に収まっていない粒子は除外
		if (r_sq < h_sq) {
			density += CalculateDensity(r_sq);	// わざわざルートをとる必要がない
		}
	}

	// 密度バッファを更新
	_ParticlesDensityBufferWrite[P_ID].density = density;
}


//--------------------------------------------------------------------------------------
// 圧力の算出
//--------------------------------------------------------------------------------------
[numthreads(THREAD_SIZE_X, 1, 1)]
void PressureCS(uint3 DTid : SV_DispatchThreadID) {
	uint P_ID = DTid.x;	// 現在処理しているパーティクルID

	float  P_density = _ParticlesDensityBufferRead[P_ID].density;	// 先程算出した密度
	float  P_pressure = CalculatePressure(P_density);	// 圧力の計算

	// 圧力バッファを更新
	_ParticlesPressureBufferWrite[P_ID].pressure = P_pressure;
}


//--------------------------------------------------------------------------------------
// 力(加速度)の算出
//--------------------------------------------------------------------------------------
[numthreads(THREAD_SIZE_X, 1, 1)]
void ForceCS(uint3 DTid : SV_DispatchThreadID) {
	uint P_ID = DTid.x; // 現在処理しているパーティクルID

	float3 P_position = _ParticlesBufferRead[P_ID].position;
	float3 P_velocity = _ParticlesBufferRead[P_ID].velocity;
	float  P_density = _ParticlesDensityBufferRead[P_ID].density;		// 先程算出した密度
	float  P_pressure = _ParticlesPressureBufferRead[P_ID].pressure;	// 先程算出した圧力

	const float h_sq = _Smoothlen * _Smoothlen;

	// 近傍探索(O(n^2))
	float3 press = float3(0, 0, 0);
	float3 visco = float3(0, 0, 0);
	for (uint N_ID = 0; N_ID < _NumParticles; N_ID++) {
		if (N_ID == P_ID) continue;	// 自身を対象とした場合スキップ

		float3 N_position = _ParticlesBufferRead[N_ID].position;

		float3 diff = N_position - P_position;
		float r_sq = dot(diff, diff);

		// 半径内に収まっていない粒子は除外
		if (r_sq < h_sq) {
			float  N_density = _ParticlesDensityBufferRead[N_ID].density;
			float  N_pressure = _ParticlesPressureBufferRead[N_ID].pressure;
			float3 N_velocity = _ParticlesBufferRead[N_ID].velocity;
			float  r = sqrt(r_sq);

			// 圧力項
			press += CalculateGradPressure(r, P_pressure, N_pressure, N_density, diff);

			// 粘性項
			visco += CalculateLapVelocity(r, P_velocity, N_velocity, N_density);
		}
	}

	// 統合
	float3 force = press + _Viscosity * visco;

	// 加速度バッファの更新
	_ParticlesForceBufferWrite[P_ID].acceleration = force / P_density;
}

// 熱拡散の算出
//--------------------------------------------------------------------------------------
[numthreads(THREAD_SIZE_X, 1, 1)]
void ThermalDiffuseCS(uint3 DTid : SV_DispatchThreadID) {
	uint P_ID = DTid.x;	// 現在処理しているパーティクルID
	int  P_type = _ParticlesTypeBufferRead[P_ID].type;
	if (P_type == 2) return;


	float3 P_position = _ParticlesBufferRead[P_ID].position;
	float  P_density = _ParticlesDensityBufferRead[P_ID].density;
	float  P_temperature = _ParticlesTemperatureBufferRead[P_ID].temperature;
	float  P_fuel = _ParticlesTemperatureBufferRead[P_ID].fuel;

	const float h_sq = _Smoothlen * _Smoothlen;

	// 化学反応の計算 (炎粒子の場合のみ)

	float thermalDiffuse = 0; //熱拡散
	float fuelDiffuse = 0; //燃料拡散

	// 燃料
	P_fuel += _FuelConsumeFactor*_ReactSpeed*P_fuel*P_temperature;
	// 温度
	P_temperature += _TemperatureProduce*_ReactSpeed*P_fuel*P_temperature;

	// 近傍探索(O(n^2))
	for (uint N_ID = 0; N_ID < _NumParticles; N_ID++) {
		if (N_ID == P_ID) continue;	// 自身を対象とした場合スキップ

		float3 N_position = _ParticlesBufferRead[N_ID].position;
		float N_temperature = _ParticlesTemperatureBufferWrite[N_ID].temperature;
		float N_fuel = _ParticlesTemperatureBufferWrite[N_ID].fuel;
		float N_density = _ParticlesDensityBufferRead[N_ID].density;

		float3 diff = N_position - P_position;
		float r_sq = dot(diff, diff);

		// 半径内に収まっていない粒子は除外
		if (r_sq < h_sq) {

			//熱拡散の計算
			float td_curr = _CopperDiffusivity*_ParticleMass*_LapViscosityCoef;
			td_curr *= N_temperature - P_temperature;
			td_curr *= (h_sq - r_sq) / N_density; //熱拡散項
			thermalDiffuse += td_curr;

			//燃料拡散の計算
			float fd_curr = 0.5f*_FuelDiffuse*_ParticleMass*_LapViscosityCoef;//燃料放射(係数部分) ※ベクトル以外
			fd_curr *= N_temperature + P_temperature;
			fd_curr *= N_fuel - P_fuel;
			fd_curr *= (h_sq - r_sq) / N_density;
			fuelDiffuse += fd_curr;
		}
	}

	_ParticleThermalDiffuseWrite[P_ID].thermalDiffuse = thermalDiffuse;
	_ParticleThermalDiffuseWrite[P_ID].fuelDiffuse = fuelDiffuse;
}

//粒子の判定(炎 / 星)
//fireflg = 2 : 星粒子 | fireflg = 1 : 仮想炎粒子 | fireflg = 0 : 炎粒子
//--------------------------------------------------------------------------------------
[numthreads(THREAD_SIZE_X, 1, 1)]
void TypeCS(uint3 DTid : SV_DispatchThreadID) {
	uint P_ID = DTid.x;	// 現在処理しているパーティクルID

	float3 P_initPos = _ParticlesInitPosBufferRead[P_ID].position;
	float P_thermalDiffuse = _ParticleThermalDiffuseRead[P_ID].thermalDiffuse;
	int  P_type = _ParticlesTypeBufferWrite[P_ID].type;
	float  P_temperature = _ParticlesTemperatureBufferWrite[P_ID].temperature;
	float  P_fuel = _ParticlesTemperatureBufferWrite[P_ID].fuel;


	if (P_type != 2) {
		// 燃料
		P_fuel += _FuelConsumeFactor*_ReactSpeed*P_fuel*P_temperature;
		// 温度
		P_temperature += _TemperatureProduce*_ReactSpeed*P_fuel*P_temperature;
		// 熱輻射(放射熱エネルギー)の計算
		float t = pow(P_temperature, 4) - pow(_AmbientTemperature, 4);
		float er = _Emissivity*_StefanConst*t;
		//温度を更新 (炎粒子の場合のみ)
		P_temperature += P_thermalDiffuse + er;
	}
	if (_StarRadius*_StarRadius < dot(P_initPos, P_initPos) && P_type == 1) {
		P_type = 0;
		P_temperature = 2000.0f;
		P_fuel = 100.0f;
	}
	if ((_StarRadius - _ParticleDist)*(_StarRadius - _ParticleDist) < dot(P_initPos, P_initPos) && P_type == 2) {
		P_type = 1;
		P_temperature = 2000.0f;
		P_fuel = 100.0f;
	}

	_ParticlesTypeBufferWrite[P_ID].type = P_type;
	_ParticlesTemperatureBufferWrite[P_ID].temperature = P_temperature;
	_ParticlesTemperatureBufferWrite[P_ID].fuel = P_fuel;
}

//--------------------------------------------------------------------------------------
// 衝突判定などを行って、位置を更新
//--------------------------------------------------------------------------------------
[numthreads(THREAD_SIZE_X, 1, 1)]
void IntegrateCS(uint3 DTid : SV_DispatchThreadID) {
	const unsigned int P_ID = DTid.x; // 現在処理しているパーティクルID

	// 更新前の位置と速度
	float3 position = _ParticlesBufferRead[P_ID].position;
	float3 velocity = _ParticlesBufferRead[P_ID].velocity;
	float3 acceleration = _ParticlesForceBufferRead[P_ID].acceleration;	// 計算した加速度

	// マウスインタラクション
	if (distance(position, _MousePos.xyz) < _MouseRadius && _MouseDown) {
		float3 dir = position - _MousePos.xyz;
		float pushBack = _MouseRadius - length(dir);
		acceleration += 100 * pushBack * normalize(dir);
	}

	// 衝突判定を書くならここ -----

	// 壁境界(ペナルティ法)
	float dist = position.x;
	acceleration += min(dist, 0) * -_WallStiffness * float3(1, 0, 0);

	dist = position.y;
	acceleration += min(dist, 0) * -_WallStiffness * float3(0, 1, 0);

	dist = position.z;
	acceleration += min(dist, 0) * -_WallStiffness * float3(0, 0, 1);

	dist = _Range.x - position.x;
	acceleration += min(dist, 0) * -_WallStiffness * float3(-1, 0, 0);

	dist = _Range.y - position.y;
	acceleration += min(dist, 0) * -_WallStiffness * float3(0, -1, 0);

	dist = _Range.z - position.z;
	acceleration += min(dist, 0) * -_WallStiffness * float3(0, 0, -1);

	// 浮力の加算
	if (_IsFirework) {
		float3 P_buoyancy = float3(0, _BuoyancyCoef*(_ParticlesTemperatureBufferRead[P_ID].temperature - _AmbientTemperature), 0);
		acceleration += P_buoyancy;
	}
	// 重力の加算
	acceleration += _Gravity;


	// 前進オイラー法で次の粒子位置を更新
	velocity += _TimeStep * acceleration;
	position += _TimeStep * velocity;

	// パーティクルのバッファ更新
	_ParticlesBufferWrite[P_ID].position = position;
	_ParticlesBufferWrite[P_ID].velocity = velocity;
}